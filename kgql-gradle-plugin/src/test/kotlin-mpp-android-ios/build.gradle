plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'com.android.library'
    id 'kotlinx-serialization'
    id 'com.codingfeline.kgql'
}

apply from: '../../../../gradle/dependencies.gradle'

repositories {
    maven {
        url "file://${projectDir.absolutePath}/../../../../build/localMaven"
    }
    google()
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
}

android {
    compileSdkVersion versions.compileSdk

    lintOptions {
        textReport true
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
        }
    }
}

kgql {
    packageName = "com.sample"
    typeMapper = [
            "UserProfile": "com.sample.data.UserProfile"
    ]
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation deps.kotlin.stdlib.common
                implementation deps.kotlin.serialization.common
            }
        }
        androidMain {
            dependencies {
                implementation deps.kotlin.stdlib.jdk7
                implementation deps.kotlin.serialization.jvm
            }
        }
        jsMain {
            dependencies {
                implementation deps.kotlin.stdlib.js
                implementation deps.kotlin.serialization.js
            }
        }
        nativeMain {
            dependencies {
                implementation deps.kotlin.serialization.native
            }
        }
        nativeTest {}
    }
    targets {
        fromPreset(presets.android, 'android')
        fromPreset(presets.js, 'js')
        fromPreset(presets.iosArm64, 'iosArm64') {
            binaries {
                framework()
            }
        }
        fromPreset(presets.iosX64, 'iosX64') {
            binaries {
                framework()
            }
        }
    }

    configure([targets.iosX64, targets.iosArm64]) {
        compilations.main.source(sourceSets.nativeMain)
        compilations.test.source(sourceSets.nativeTest)
    }
}
