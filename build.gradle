apply plugin: 'com.github.ben-manes.versions'

buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"

    repositories {
        // prevent gradle from re-downloading kotlin/native's POM upon sync
        // https://youtrack.jetbrains.com/issue/KT-28128
        mavenCentral().content() { excludeGroup "Kotlin/Native" }
        google().content() { excludeGroup "Kotlin/Native" }
        jcenter {
            content {
                excludeGroup "Kotlin/Native"
            }
        }
        gradlePluginPortal()
        maven {
            url 'https://dl.bintray.com/kotlin/kotlinx'
            content {
                excludeGroup "Kotlin/Native"
            }
        }
        maven {
            url 'https://dl.bintray.com/kotlin/kotlin-eap'
            content {
                excludeGroup "Kotlin/Native"
            }
        }
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }


        dependencies {
            classpath deps.plugins.kotlin
            classpath deps.plugins.serialization
            classpath deps.plugins.dokka
            classpath deps.plugins.android
            classpath deps.plugins.versions
            classpath deps.plugins.pluginPublish
        }
    }
}


allprojects {
    repositories {
        google().content() { excludeGroup "Kotlin/Native" }
        mavenCentral().content() { excludeGroup "Kotlin/Native" }
        jcenter {
            content {
                excludeGroup "Kotlin/Native"
            }
        }
        maven {
            url 'https://dl.bintray.com/kotlin/kotlinx'
            content {
                excludeGroup "Kotlin/Native"
            }
        }
        maven {
            url 'https://dl.bintray.com/kotlin/kotlin-eap'
            content {
                excludeGroup "Kotlin/Native"
            }
        }
    }

    group = GROUP
    version = VERSION_NAME
}

wrapper {
    gradleVersion = '5.1.1'
    distributionType = Wrapper.DistributionType.ALL
}

// TODO work around https://youtrack.jetbrains.com/issue/KT-27059
@SuppressWarnings(["UnnecessaryQualifiedReference", "unused", "GrMethodMayBeStatic"])
def addDependency(Upload task, String groupId, String artifactId, String version, String type) {
    task.repositories.withType(org.gradle.api.artifacts.maven.MavenDeployer) { mavenDeployer ->
        task.doFirst {
            mavenDeployer.pom.whenConfigured {
                // For whatever reason we can't 'new' these ourselves. Clone an existing instance instead.
                def dependency = it.dependencies.get(0).clone()
                dependency.groupId = groupId
                dependency.artifactId = artifactId
                dependency.version = version
                dependency.type = type
                it.dependencies.add(dependency)
            }
        }
    }
}
